// <type>[optional scope]: <description>
//
// [optional body]
//
// [optional footer(s)]
message: title [body] [footer]

title: [type scope? break? ": "] description

?body: ("*"? MESSAGE)+

?footer: (breaking_change | issue | trailer)+

// title
type: ":"? CNAME ":"?
break: "!"
scope: "(" CNAME ")"
description: DESCRIPTION
DESCRIPTION: /[^\r\n]+/

// footer
breaking_change: "BREAKING" [" " | "-"] "CHANGE: " MESSAGE
issue: TOKEN " #" [CNAME "-"] INT
trailer: TOKEN ": " NAME "<" EMAIL ">"
TOKEN: LETTER (LETTER | "-")* LETTER
NAME: /[a-z ,.'-]+/i

// email
EMAIL: LOCAL_PART "@" DOMAIN
LOCAL_PART: (LETTER | SPECIAL_CHAR | INT | ".")+
DOMAIN: (SUBDOMAIN ".")+ TLD
SUBDOMAIN: LETTER (LETTER | "-" | INT)+ (LETTER | INT)
TLD: LETTER+

// body
MESSAGE: (CNAME | WS_INLINE | NUMBER | SPECIAL_CHAR | "@" | "," | ".")+

SPECIAL_CHAR: "!"
    | "#"
    | "$"
    | "%"
    | "&"
    | "'"
    | "*"
    | "+"
    | "-"
    | "/"
    | "="
    | "?"
    | "^"
    | "_"
    | "`"
    | "{"
    | "|"
    | "}"
    | "~"

%import common.CNAME
%import common.INT
%import common.LETTER
%import common.NEWLINE -> _NL
%import common.NUMBER
%import common.WORD
%import common.WS
%import common.WS_INLINE
%ignore WS
